#var huashan[META] {
	{NAME} {huashan}
	{DESC} {华山派辅助脚本}
	{AUTHOR} {mnikn}
};

#func {huashan.Init} {
	okLog 华山辅助脚本启动！;
	#return true;
}

#var yue-auto-job-enable {false};
#alias yue-job {
	#if {"%1" != ""} {
		#var yue-auto-job-enable {%1};
	};
	okLog 开始巡山！;
	ask yue lingshan about job;

	event.Handle {GMCP.Move} {path.Walk} {$MODULE} {yue-job-step};

	#local go_job_paths {n;n;e;sd;sd;sd;nu;nu;nu;w;nd;eu;wd;nu;n;nd;wu;ed;nw;nd;nd;nu;sd;wd;nd;wd;nd;nw;n;s};
	path.WalkSteps {$go_job_paths} {
		okLog 巡山路线结束;
		event.UnHandle {GMCP.Move} {path.Walk};

		#local return_paths {se;su;eu;su;eu;su;su;se;su;s;sd;su;s;s};
		path.WalkSteps {$return_paths} {
			give ling pai to yue lingshan;
			okLog 结束巡山！;
			
			#if {$yue-auto-job-enable} {
				#delay yue-job-delayer {
					#if (!@char.IsBusy{}) {
						yue-job $yue-auto-job-enable;
					};
					#else {
						busy.Wait {
							yue-job $yue-auto-job-enable;
						};
					};
				} 2;
			};
		}
	};
	#return; 
}
#alias remove-yue-job {
	#var yue-auto-job-enable {false};
}

#alias yue-job-step {
	path.Walk.Stop;
	
	#delay {yue-job-step-delayer} {
		path.Walk.Resume;
	} 4;
}

#alias zd {zhidian student};
#alias gen {ask student about 跟随};


#var ning-auto-job-enable {false};
#alias ning-job {
	#if {"%1" != ""} {
		#var ning-auto-job-enable {%1};
	};
	ask ning about job;
	e;e;e;
	auto-zd;
	#action {趁你不注意} {
		remove-auto-zd;
		#delay 3 {
			find-dizi;
		};
	};
	#action {有板有眼} {
		#unaction {趁你不注意};
		#unaction {有板有眼};
		remove-auto-zd;
		w;w;w;
		ask ning about finish;

		
		#if {$ning-auto-job-enable} {
			#delay ning-job-delayer {
				#if (!@char.IsBusy{}) {
					ning-job $ning-auto-job-enable;
				};
				#else {
					busy.Wait {
						ning-job $ning-auto-job-enable;
					};
				};
			} 2;
		};
	};
}
#alias remove-ning-job {
	#var ning-auto-job-enable 0;
	remove-auto-zd;
}

#alias auto-zd {
	#tick auto-zd-ticker {
		zhidian student;
	} 1;

	#action {^什么} {
		remove-auto-zd;
		#unaction {^什么};
	};
}
#alias remove-auto-zd {
	#untick auto-zd-ticker; 
	#unaction {^什么};
}

#var dizi-path {w;s;n;w;s;n;w;e;e;n;n;n;e;e;w;sd;sd;sd;nu;nu;nu;w;nd;enter;out;eu;wd;nw;wd;w;sw;ne;up;down;e;eu;se;nu;n;nd;wu;ed;n;nd;nd;w;enter;out;east;nd;su;su;su;s;nw;nd;nd;nu;sd;wd;nd;wd;nd;nw;n;s;ne;sw;se;se;enter;out;su;enter;out;sw;sw;eu;su;eu;nu;sd;su;su;e;su;sw;s;sd;su;s;s;s;e};
#alias find-dizi {
	map.FocusNPC {{id} {$user[id]'s student}} command {ning-ask-dz};
	event.Handle {GMCP.Move} {path.Walk} {$MODULE} {ning-zd-walk-time};
	path.WalkSteps {$dizi-path} {auto-zd};
	set brief 0;
}
#alias ning-ask-dz {
	#undelay walk.Resume;
	#action {%*道：好吧，好吧，我回去还不行吗？} {
		#untick ning-ask-dz-ticker;
		map.FocusNPC;
		#nop path.Walk.Stop;
		path.Walk.Resume;
	};
	#tick {ning-ask-dz-ticker} {ask student about 跟随;} {2};
}

#alias ning-zd-walk-time {
	#if {"$gGMCP[Move][房间名]" == "练功房"} {
		path.end;
		event.UnHandle {GMCP.Move} {path.Walk};
		#return;
	};
	path.Walk.Stop;
	#if {"$gGMCP[Move][房间名]" == "栈道"} {
		#delay walk.Resume {path.Walk.Resume} 2.0;
		#return;
	};

	#delay walk.Resume {path.Walk.Resume} 0.2;
}
