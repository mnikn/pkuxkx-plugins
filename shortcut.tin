#var shortcut[META] {
	{NAME} {shortcut}
	{DESC} {通用快捷键}
	{AUTHOR} {mnikn}
}

#func shortcut.Init {
	#return true;
}


#alias auto-xue {
	#var auto-xue-target-person %1;
	#var auto-xue-target-skill %2;
	#var auto-xue-times 10;

	#echo 自动学习开始！;
	#tick auto-xue-ticker {
		#if {@char.IsBusy{}} {
			#return;
		};

		
		#if {$char[HP][精神百分比] > 80} {
			xue $auto-xue-target-person for $auto-xue-target-skill $auto-xue-times;
		};
		#else {
			#if {$char[HP][当前内力] < 50} {
				#return;	
			};
			#if {$char[HP][内力百分比] > 45} {
				yun regenerate;
			};
		};
		
	} 1;

	#act {缺乏实战经验} {remove-auto-xue;};
}
#alias remove-auto-xue {
	#untick auto-xue-ticker;
	#echo 自动学习结束！; 
}

#alias dazuo-inc {
	dazuo max;
	dazuo 10;
}
#alias auto-dazuo {
	okLog 自动打坐开始！;
	#tick auto-dazuo-ticker {
		#if {@char.IsBusy{} || $char[HP][精神百分比] < 80} {
			#return;
		};

		#action {无法产生内息运行全身经脉。$} {remove-autodazuo};
		#action {你现在内力接近圆满} {
			#if {@char.IsBusy{}} {
				#return;
			};
			dazuo 10;
		};

		#if { $char[HP][内力百分比] < 99 } {
			#if {$char[HP][气血百分比] > 80} {
        			dazuo max;
			};
    		};
    		#else {
        		dazuo 10;
    		};
	} 1;
}

#alias remove-auto-dazuo {
	#untick auto-dazuo-ticker;
	okLog 自动打坐结束！;
}


#alias auto-tuna {
	okLog 自动吐纳开始！;
	#tick auto-dazuo-ticker {
		#if {@char.IsBusy{} || $char[HP][气血百分比] < 80 || $char[HP][精神百分比] < 80} {
			#return;
		};

        	tuna max;
	} 5;
}
#alias remove-auto-tuna {
	#untick auto-tuna-ticker;
	okLog 自动吐纳结束！;
}


#alias auto-fullstatus {
	okLog 自动补满状态启动！;
	#tick auto-fullstatus-ticker {
		#if {@char.IsBusy{}} {
			#return;
		};
		
		#if {$char[HP][内力百分比] < 99 && $char[HP][气血百分比] > 80 && $char[HP][精神百分比] > 80} {
			dazuo max;
		};

		#if {${char[HP][饮水] < 150}} {
			drink;
			drink;
			drink;
		}
	} 1;
}

#alias remove-auto-fullstatus {
	#untick auto-fullstatus-ticker;
	okLog 自动补满状态结束！;
}
